### Language
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

### Check if drugpath is specified
if(NOT DEFINED DRUGSPATH)
    message(FATAL_ERROR "The variable DRUGSPATH should be set when invoking cmake. Like : cmake -DDRUGSPATH=<somepath>/tucuxi-drugs/drugfiles")
endif(NOT DEFINED DRUGSPATH)


### Configure and add subdirectories
set(TUCUXI_BASE_DIR_INCLUDE ${CMAKE_CURRENT_SOURCE_DIR})

add_subdirectory(admin/)
get_target_property(ADMIN_INCLUDES admin INTERFACE_INCLUDE_DIRECTORIES)

add_subdirectory(apputils/)
get_target_property(APPUTILS_INCLUDES apputils INTERFACE_INCLUDE_DIRECTORIES)

add_subdirectory(core/)
get_target_property(CORE_INCLUDES core INTERFACE_INCLUDE_DIRECTORIES)

add_subdirectory(databases/)
get_target_property(DATABASES_INCLUDES databases INTERFACE_INCLUDE_DIRECTORIES)

add_subdirectory(flatrequests/)
get_target_property(FLATREQUESTS_INCLUDES flatrequests INTERFACE_INCLUDE_DIRECTORIES)

add_subdirectory(guiutils/)
get_target_property(GUIUTILS_INCLUDES guiutils INTERFACE_INCLUDE_DIRECTORIES)

add_subdirectory(processingtucucore/)
get_target_property(PROCESSINGTUCUCORE_INCLUDES processingtucucore INTERFACE_INCLUDE_DIRECTORIES)

add_subdirectory(rest/)
get_target_property(REST_INCLUDES rest INTERFACE_INCLUDE_DIRECTORIES)


### Get GUI stuff
# Add linker flag, else the executable is not clickable in Ubuntu
if(UNIX)
    set(TUCUXI_LFLAGS ${TUCUXI_LFLAGS} -no-pie)
endif(UNIX)

## Set logo
set(TUCUXI_APP_ICON ${CMAKE_CURRENT_SOURCE_DIR}/guiutils/resources/icons/logo.ico)

## Set Resources
if(config_demo)
    set(TUCUXI_APP_RESOURCES ${CMAKE_CURRENT_SOURCE_DIR}/gui/resources/demo/demo.qrc)
endif(config_demo)

## TODO Config gui tests
if(config_guitest)
    ### TODO
endif(config_guitest)

set(GUI_INCLUDES_DIRS
        ${CMAKE_CURRENT_SOURCE_DIR}/gui
        ${CMAKE_CURRENT_SOURCE_DIR}/gui/src)

## Sources
set(GUI_SOURCE_FILES
        ${CMAKE_CURRENT_SOURCE_DIR}/gui/src/main.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/gui/src/restconfigdialog.cpp
        ${CMAKE_CURRENT_SOURCE_DIR}/gui/src/licensedialog.cpp
)

## Install the definition xml file
set(DEFINITION_PATH ${CMAKE_INSTALL_BINDIR})
if(APPLE)
    set(DEFINITION_PATH ${CMAKE_INSTALL_BINDIR}/${PROJECT_NAME}.app/Contents/Resources/)
endif(APPLE)
if(ANDROID)
    set(DEFINITION_PATH /assets/)
endif(ANDROID)

set(DEFINITION_FILES ${CMAKE_CURRENT_SOURCE_DIR}/core/xml/definitions.xml)

## Install the definition tdd file
if(NOT config_demo)
    set(DRUGS_TO_PATH ${CMAKE_INSTALL_BINDIR}/drugfiles)
    if(APPLE)
        set(DRUGS_TO_PATH ${CMAKE_INSTALL_BINDIR}/${PROJECT_NAME}.app/Contents/Resources/drugfiles)
    endif(APPLE)
    if(ANDROID)
        set(DRUGS_TO_PATH /assets/drugfiles)
    endif(ANDROID)

    # Copy command (TODO -> Not the whole command from gui.pro was translated here)
    # It copies to the root of build directory, not where tucuxi is generated
    file(COPY ${DRUGSPATH} DESTINATION ${CMAKE_BINARY_DIR})
    #file(COPY ${DRUGSPATH} DESTINATION ${DRUGS_TO_PATH})
endif(NOT config_demo)

#add_custom_command(tucuxi ephemeraldb POST_BUILD COMMAND copy ${ephemeraldbdll} ${ephemeraldb})

### Building
qt_add_executable(tucuxi
    current_version.h
    ${GUI_SOURCE_FILES}
    # Translation files generated by Qt via qt_add_translation
    ${QM_FILES}
)

## Set WIN32 for windows to avoid console getting opened on start on windows
if(WIN32)
    set_target_properties(tucuxi PROPERTIES WIN32_EXECUTABLE true)
endif()

target_link_libraries(tucuxi
    PRIVATE
        admin
        apputils
        core
        flatrequests
        guiutils
        processingtucucore
        rest
        ${QT_COMPONENTS}
)

target_include_directories(tucuxi
    PRIVATE
        ${ADMIN_INCLUDES}
        ${APPUTILS_INCLUDES}
        ${CORE_INCLUDES}
        ${DATABASES_INCLUDES}
        ${FLATREQUESTS_INCLUDES}
        ${GUI_INCLUDES_DIRS}
        ${GUIUTILS_INCLUDES}
        ${PROCESSINGTUCUCORE_INCLUDES}
        ${REST_INCLUDES}
        
	${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/src
	
	${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/boost
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/spdlog/include
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/eigen
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/date/include
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/rapidxml
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/rapidjson/include
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/botan/build/include
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/cxxopts/include
                    ${CMAKE_CURRENT_LIST_DIR}/../libs/tucuxi-core/libs/tiny-js

        ${TUCUXI_BASE_DIR_INCLUDE}
)

## Set compiler and linker flags
# Debug Mode
target_compile_options(tucuxi PRIVATE "$<$<CONFIG:DEBUG>:${TUCUXI_CXXFLAGS};-O0;-g>")
# Release Mode
target_compile_options(tucuxi PRIVATE "$<$<CONFIG:RELEASE>:${TUCUXI_CXXFLAGS};-O3>")

# Activate QML debug
target_compile_definitions(tucuxi
    PRIVATE
        "$<$<CONFIG:DEBUG>:QT_QML_DEBUG>"
)

target_link_options(tucuxi PRIVATE ${TUCUXI_LFLAGS})

# Install rules
include(GNUInstallDirs)
install(TARGETS tucuxi
        LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
        RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)
install(FILES ${DEFINITION_FILES}
        DESTINATION ${DEFINITION_PATH}
)

